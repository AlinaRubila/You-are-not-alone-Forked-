# Начало третьей главы рута Жени

image cj_canteen_timur_blur = im.Blur("cj/cj_canteen_timur.png", 1.5)


label jenya_cp3:

    scene black with dissolve

    centered "{size=+24}{color=#ffffe0}Глава 3"

    #Звук птиц за окном. (весенняя спокойная мелодия).
    play music leaves
    play background birds

    scene bg_room_dawn with dissolve

    "Время идёт, и наступает весна, как пелось в одной известной песне «Чумачечая»."

    "На улицах появляются лужи. Мне всё чаще встречаются могучие кораблики из бумаги, рассекающие ручейки на асфальте. Люди же стали ходить с расстегнутыми куртками, не пряча свои новые прически под шапками."

    "Вместе с холодами отступила и моя проблема — те странные сновидения и происшествия."

    "Я про таинственную девушку, вечно вторгающуюся в мою жизнь и заставляющую покачнуться мою уверенность в том, что я адекватный и не страдающий какими-либо галлюцинациями человек."

    "Я сделал вывод, что мне помогает Женин амулет."

    "Быть может, это что-то типа плацебо — я думаю об этом амулете или о том, кто его сделал, и эти мысли вымещают мысли о призраке."

    "А может, Женя тоже, сама того не осознавая, является частью чего-то таинственного и магического. А может, абсолютно все мы часть этой загадки."

    "К слову о загадках, мой сосед — это бермудский треугольник, если бы последний был человеком."

    "Все мои попытки с ним заговорить заканчивались угрюмым односложным ответом или многозначительным подъёмом бровей."

    "Так что, будучи бок о бок с этим человеком на протяжении вот уже многих месяцев, я сделал вывод, что молчание — это его язык."

    "Поэтому за последнее время я сказал ему три или четыре предложения, как максимум, в остальное время мы общались взглядами и мимикой, считай телепатически…"

    stop music fadeout 1.0
    stop background fadeout 1.0
    scene black with dissolve
    play music knowledge
    play background people

    scene cj_canteen_timur with dissolve

    "На сегодняшний день пары благополучно закончились, а Женя куда-то успела скрыться."

    "Я же решил отобедать в столовой сэндвичами, которые она заботливо приготовила для меня."

    "Женя — опора моего желудка."

    "Мы с ней постоянно ужинаем вместе тем, что она приготовит под какой-нибудь сериал."

    "Я не держу её в кулинарном рабстве. Всё, что она готовила, было приготовлено исключительно по доброй воле!"

    "В столовой почти никого не было. Только Тимур сидел за одним из столов."

    "Я хотел было сесть за любой другой стол, но тоскливый вид моего соседа поменял мои планы."

    "Я сел напротив Тимура. Тот, в свою очередь, даже не посмотрел на меня."

    scene cj_canteen_timur_blur with dissolve

    show mc normala at left2 with dissolve

    mc "Привет, эм… как дела с подготовкой к экзаменам?"

    n "Тимур проигнорировал меня."

    show mc smile handa at left2 with dissolve

    mc "Слушай, давай на этой неделе ты уберёшься в комнате, а на следующей я?"

    n "Тимур молча кивнул и продолжил есть."

    "Я решил не отставать и тоже открыл свой сэндвич с ветчиной. Тимур ел готовую еду, а если точнее, рис с курицей и два ролла."

    "Я съел сэндвич очень быстро. Настолько, что мой желудок, судя по голоду, продолжил желать подношений."

    "Я недовольно пробурчал, не ожидая, что мне кто-то ответит."

    show mc talk crosseda at left2 with dissolve

    mc "Одного сэндвича, конечно, мало."

    n "И получил неожиданную реакцию от Тимура."

    show timur normala at right2 with dissolve

    t "Мне наоборот много. Если хочешь, можешь взять второй мой ролл. Я его ещё не трогал."

    n "Завести диалог первым — это была его катастрофическая ошибка, ибо я сразу же счёл это за призыв снова попробовать начать с ним диалог. Ничему меня жизнь не учит."

    show mc surprise handa at left2 with dissolve

    show mc smile handa at left2 with dissolve

    mc "Да, с радостью, спасибо!"

    n "Доедая его ролл, я хорошенько продумал все свои следующие ходы."

    "Сначала я…"

    mc "Очень вкусный ролл. Где ты его купил?"

    n "Он парирует."

    t "Здесь."

    n "Я в ответ, чуть погодя:"

    show mc normala at left2 with dissolve

    mc "Ты слышал, что скоро у нас по общаге пройдет дезинсекция?"

    n "Он явно на крючке."

    show timur surprisea at right2 with dissolve

    t "В комнаты тоже будут заходить?"

    n "Дальше нельзя было оплошать. Скажу «Нет», и он сорвется с крючка, ибо его это перестанет касаться, а значит, ему станет неинтересно. Но есть один нюанс. Я-то знаю, что ответ «Нет». Придётся врать, но так, чтобы было невозможно попасться."

    show mc thoughtful crosseda at left2 with dissolve

    mc "Не знаю. Может да, может нет."

    show timur normala at right2 with dissolve

    n "Он недовольно вздыхает и ничего не отвечает. Я рассчитывал обсудить потенциальную генеральную уборку комнаты, но нужно менять план."

    show mc smile handa at left2 with dissolve

    mc "Кстати о чистоте. Я сегодня видел новость, что у нас рядом с общагой один шаурмист использовал крыс в качестве мяса для шаурмы…"

    n "Стоп, что я делаю? Нужно остановиться."

    mc "Помнишь, как кто-то в беседу общаги писал, что мясо у этого шаурмиста какое-то странное?.."

    n "Эй, язык, хватит, ты все портишь!"

    mc "Так это что получается — среди нас есть крысоеды?"

    show timur sada at right2 with dissolve #не нашла серьёзность, но грусть подходит

    n "Нет! Язык-предатель!"
    hide timur sada with dissolve
    stop music fadeout 1.0

    "Из-за приступа моего словесного недержания Тимур встал и молча ушёл."

    show mc normala at left2 with dissolve

    "Ну зачем, зачем я об этом заговорил? В какой альтернативной вселенной это был бы уместный диалог в столовой? Чёрт возьми, мне даже самому есть расхотелось."

    hide mc normala with dissolve
    "Что ж… Поражения случаются. Возможно даже, случаются каждый день, что поделать. Чтобы отвлечься, я решил пойти и просто позаниматься в библиотеке."

    stop background fadeout 1.0
    play music chill
    scene bg_library with dissolve

    "Я решил почитать что-то из азиатской литературы, но такое всегда было тяжело найти, тем более в студенческой библиотеке."

    "Ходя туда-сюда между стеллажами с книгами, я не встретил ни одного человека, даже библиотекарши нигде не было."

    "Но откуда-то стало доноситься мычание, будто бы знакомое мне."

    stop music fadeout 1.0

    "Сначала мне не хотелось в это верить."

    "Я убеждал себя, что это не она."

    "Но мычание продолжалось. Назойливое, словно издевательское."

    "По моему телу пробежали мурашки, и я схватился за Женин амулет с надеждой на то, что он действительно поможет и прогонит ту, о ком я сейчас подумал."

    show mc surprisea at left2 with dissolve

    mc "Эй, это ты?"
    "Сказал я, не зная, на что рассчитываю."

    hide mc surprisea with dissolve
    play music chill

    "Мычание прекратилось. Я выдохнул и снова взглянул на амулет."

    "Возможно, этот амулет — мой ключ к спокойной жизни. Я должен быть безмерно благодарен Жене, что она спасла меня от этого проклятия."

    "Мысли о том, что всё позади, безусловно поднимают настроение."

    "Я продолжил исследовать библиотеку. Будто бы парил по ней, а не ходил."

    "Тишина. Никаких привидений."

    stop music fadeout 1.0

    "Но мой покой нарушило очередное мычание, на этот раз ещё громче, чем было раньше."

    #Звук мычания.

    "Автоматически моя рука сразу взяла амулет и подняла его в воздух, как бы показывая, что я в домике."

    "Мычание не прекращалось. Я приложил амулет к себе и ускорил шаг, планируя подойти к углу и использовать амулет там."

    "Быстрым шагом, почти бегом, аккуратно и испуганно я направился к краю библиотеки и повернул за угол стены, вздрогнув от увиденного."

    play music home

    "Прямо за углом всё это время сидела Женя, нависающая над какой-то книгой, и громко мычала."

    "Всё это время, по-видимому, мычание издавалось от неё и её тяжелых мыслительных процессов."

    "В ушах у неё были наушники. Видимо, она из тех, кого не напрягают лишние шумы во время учёбы."

    "Я аккуратно потрогал её за плечо, от чего она вздрогнула и громко выругалась."

    "Женя развернулась и замахнулась кулаком, но в последний момент остановилась."

    show jenya surprisea at right2 with dissolve

    j "Эй, я же могла тебя ударить ненароком! Нельзя же так пугать!"

    n "Ничего себе реакция."

    show mc surprisea at left2 with dissolve

    mc "Прости, прости. Меня привлекло твоё громкое мычание."
    show mc smilea at left2 with dissolve

    n "Видимо, я слегка улыбнулся, так как Женя агрессивно на меня посмотрела."

    show jenya angrya at right2 with dissolve

    j "И что, смешно? Я забавно мычу, верно?"

    show mc sada at left2 with dissolve

    mc "Нет, просто…"

    j "Я тут сижу битый час. Мне сейчас не до твоего странного чувства юмора."

    show jenya normala at right2 with dissolve

    j "И вообще, я всегда мычу, когда думаю. Кто-то щёлкает ручкой, кто-то мычит. Что ж теперь?"

    show mc normal handa at left2 with dissolve

    mc "Кое-кто явно не в настроении."

    show jenya angrya at right2 with dissolve

    j "Я ненавижу этот предмет. Всей душой. Видит Бог, как же я терпеть не могу за ним сидеть."

    show mc blush handa at left2 with dissolve

    mc "Если хочешь, я могу загладить вину за этот смешок и помочь тебе."

    show jenya blusha at right2 with dissolve

    j "Нет. Вернее… Да, помоги, будь добр. Но по дружбе. А не потому, что ты виноват. Ты вообще не виноват. Прости, вспылила."

    mc "Всё норм."

    hide mc blush handa with dissolve
    hide jenya blusha with dissolve

    n "Подставив стул, я сел рядом и первым делом решил узнать, что Женя уже сделала и что конкретно она не понимает."

    "Взяв ее тетрадь, я листал назад до момента, пока не сменилась дата, и так пролистал 15 страниц, не меньше."

    "Она действительно выкладывается на полную: у меня по этому предмету 15 страниц набралось за все полсеместра."

    show mc normal handa at left2 with dissolve
    mc "В общем, смотри…"

    hide mc normal handa with dissolve

    n "И я принялся ей объяснять, в чём конкретно дело, делая перерывы на каждый её вопрос."

    "А вопросов у неё была тьма-тьмущая. Иногда они даже повторялись, что изредка заставляло меня недовольно вздыхать."

    #Музыка становится тише.
    play sound pen
    stop music fadeout 2.0

    "Заметив это, Женя отбросила ручку и громко спросила:"

    show jenya sada at right2 with dissolve
    play music sadness

    j "Я тупая?"

    show mc surprisea at left2 with dissolve
    mc "Чего? Нет, просто тебе это тяжело дается. Это нормально."

    j "Мне абсолютно все предметы тяжело даются."

    show mc normal handa at left2 with dissolve
    mc "Ты отлично учишься по английскому."

    j "Я имею в виду профильные предметы. Все эти коды, числа, массивы… Я их терпеть не могу!"

    play sound table
    n "На этом моменте она ударила рукой по столу."

    mc "Жень, может тебе это…"

    n "Легко дать совет перевестись, но только если тебе всё равно на человека."

    "Если человек тебя волнует, то, сказав такое, ты берёшь на себя ответственность за его будущее решение."

    "Даже, чтобы просто посоветовать перевод, уже нужна большая решимость, что уж говорить о самом факте перевода."

    mc "Ну, перевестись? Я, конечно, понимаю, что это тяжело, но ведь нужно бороться за своё счастье."

    show jenya normala at right2 with dissolve
    j "Желаешь от меня избавиться?"

    n "Она посмотрела на меня со всей серьёзностью, и этот взгляд будто окатил меня ледяной водой. В голове крутилось множество слов для оправдания, но предложений не выходило."

    "Женя точно заметила моё напряжение и решила окончить мои мучения явно натянутой улыбкой."

    show jenya smilea at right2 with dissolve
    j "Да шучу я, шучу. Как же ты тут без меня."

    show mc smile crosseda at left2 with dissolve
    mc "Зная, что ты на пути к своему счастью, как-нибудь справлюсь."

    mc "Может быть, сейчас ты и ошиблась с направлением, но, пробуя себя в разном, когда-нибудь ты найдешь то, чем действительно хочешь заниматься. Главное — следовать своему сердцу."

    show jenya sada at right2 with dissolve
    show mc sad crosseda at left2 with dissolve

    n "Улыбка исчезла, остался только печальный взгляд."

    j "Я понимаю. Я всё прекрасно понимаю."

    n "Женя из тех людей, которые, когда они смеются, заставляют смеяться и тебя."

    "Если она улыбается, улыбка возникает и у тебя. Если она грустит, на тебя тоже накатывает печаль. Так и сейчас: я будто впитал её эмоции в себя, хотя и не хотел подавать виду."

    "Я взял Женю за руку в попытке поддержать."

    j "Это направление вообще не моё, учиться здесь — просто мучение. Ты не думай… я пыталась, правда, но ничего не выходит."

    show mc normala at left2 with dissolve
    mc "Мне тоже прошлая специальность не слишком нравилась, поэтому и перевёлся. Важно то, как ты себя чувствуешь. Если так и будешь себя заставлять — ничего хорошего не выйдет."

    j "Может, действительно стоит перевестись…"

    n "Я заметил, как она задумалась."

    j "Так, всё, хватит о грустном."
    stop music fadeout 2.0
    show jenya normal smilea at right2 with dissolve

    n "Она попыталась улыбнуться."

    #Музыка плавно меняется на обычную, хорошую.

    play music leaves fadein 1.0

    show mc normal crosseda at left2 with dissolve

    mc "Не волнуйся, всё решится и будет просто зашибись. Давай возвращаться в общагу?"

    show jenya smilea at right2 with dissolve

    j "Давай. Спасибо за поддержку."

    scene bg_uni_day with dissolve

    show jenya normal smilea at right2 with dissolve

    j "Ты с Тимуром так и не поговорил нормально?"

    show mc sada at left2 with dissolve

    mc "Не, так и не было нормального общения."

    show jenya normala at right2 with dissolve

    j "Он хороший, просто устаёт на работе. Всё наладится."

    j "Моя соседка тоже славная, очень помогает с домашкой и в общении с преподавателями."

    show mc smile crosseda at left2 with dissolve
    mc "Насчёт ужина есть идеи?"

    j @smile "Предлагаю зайти в магазин за продуктами и что-то состряпать. Как тебе?"

    mc "Я не против. Благодаря тебе я стал лучше готовить, спасибо."

    n "Я и не заметил, как уроки готовки стали для нас регулярными. Женя учила меня многим своим любимым рецептам, а я чётко следовал её указаниям."

    "Не знаю, из приличия ли Женя хвалила меня или мои кулинарные навыки действительно улучшились. Наверное, не зря учителя в школе называли меня «умным, но ленивым».  Может, я и поваром мог бы стать, если бы не ленился!"

    stop music fadeout 2.0

    scene black with dissolve
    pause 1
    play music sunshine

    scene bg_room_dawn at pan with dissolve

    "Я проснулся. Вчера мы с Женей договорились опять приготовить завтрак вместе. Я умылся, почистил зубы и пошёл на кухню. Обычно в это время Женя уже была там."

    scene bg_kitchen_morning with dissolve
    "Но, когда я пришёл, её не было. Странно…"

    "Я зашёл на её страничку ВК и увидел, что она была онлайн очень поздно. Наверное, опять училась допоздна. Ей так тяжело это даётся. Бедная, устала, наверное."

    "А сделаю-ка я ей сюрприз. Думаю, она обрадуется, если я приготовлю ей завтрак. Только что же мне сделать?"

    menu:
        "Приготовить блинчики":

            "Точно! Помню, она говорила, что любит блины."

            "Я нашёл рецепт в Интернете. К счастью, все продукты у меня были. Было немного страшно, так как я ни разу до этого не делал блины, но ради Жени я постараюсь."

            "Я смешал яйца, соль, сахар, муку и молоко в миске. Тесто, на удивление, получилось однородным и вкусным. Я вылил немного теста на сковороду, и, конечно, ничего не получилось."

            "Но я, не отчаиваясь, попробовал снова. Спустя 3-4 провала блины начали получаться. Неровные, рвущиеся, но вкусные."
            scene cj_pancake with dissolve

            "Когда закончилось тесто, я красиво выложил блины на тарелку, убрал за собой и в предвкушении направился к Жене. Это странно, но я испытывал сильное волнение и переживал за то, как она отреагирует. Я очень хотел её порадовать."
            stop music fadeout 2.0

            $ relate_jenya += 15
            $ favorite_food = True
            play music together fadein 1.0

        "Приготовить овсянку":

            scene cj_porige with dissolve

            "Я пока не до конца уверен в своих кулинарных способностях. Поэтому решил, что лучше просто приготовлю ей овсянку."

            "Я сварил её в кастрюле и украсил ягодами. Получилось достаточно симпатично. Думаю, Жене должно понравиться."

            "Я убрал за собой на кухне. Переживания за Женю меня не покидали. Успокоившись, я пошёл к комнате моей подруги."
            stop music fadeout 2.0

            #Музыка более приглушённая.
            play music sincerely fadein 1.0

    scene bg_hallway_day with dissolve

    "Я стоял у её двери. Набравшись сил, я постучал в дверь, чуть не уронив всё приготовленное."

    "Женя не открывала. Мне стало немного неловко — вдруг я её разбужу? Я уже был готов уходить, как вдруг дверь всё же открылась."

    show jenya yawn homea at right2 with dissolve
    "Женя стояла передо мной. Она выглядела очень потерянной и уставшей."

    if favorite_food:
        "Зря она, конечно, себя так изматывает."

        "Я понял, что хочу помогать ей чаще: она же так устаёт. Да и Женя всегда на моей стороне. Надо почаще устраивать ей сюрпризы!"

        show jenya normal smile handsa at right2 with dissolve

        j "Спасибо большое, ты мне очень помогаешь. Я так устаю, постоянно учу что-то ночью."

        show mc normal homea at left2 with dissolve
        mc "Ё-моё, каждую ночь? Будь аккуратней, так и до больницы недолго. Давай хотя бы через день."

        show jenya normal homea at right2 with dissolve
        j "Ладно, хорошо. Просто за экзамен переживаю очень сильно."

        show mc smile homea at left2 with dissolve
        mc "Тогда будем вместе готовиться, отказы не принимаются!"

        show jenya smile2 homea at right2 with dissolve
        j "Хорошо, хорошо."

        play music wholesome

    else:

        show jenya sad homea at right2 with dissolve

        j "О, спасибо, на вид очень вкусно."

        n "Женя вроде даже удивилась моему сюрпризу, но её реакция была достаточно безэмоциональной. Зря она, конечно, себя так изматывает."

        "Я понял, что хочу помогать ей чаще: она же так устаёт. Да и Женя всегда на моей стороне. Надо почаще устраивать ей сюрпризы!"

        show mc smile homea at left2 with dissolve

        mc "Надеюсь, это придаст тебе сил. Вечером будем вместе готовиться."

        show jenya smile2 homea at right2 with dissolve
        j "Хорошо, спасибо за завтрак. Увидимся вечером."

    scene bg_room_dawn with dissolve
    "Я вернулся домой. Переживания за Женю сменились рутинной учёбой."

    "Я дописал несколько учебных работ. Затем прибрался в комнате перед приходом Жени. Даже удобно, что Тимур часто не дома — ко мне могут приходить гости."

    scene black with dissolve
    scene bg_room_evening with dissolve

    play sound stuk

    if favorite_food:
        "В дверь постучали."
        "Я открыл дверь, там стояла Женя. Она выглядела гораздо свежее, чем утром. На её лице сияла улыбка, и меня порадовало её приподнятое настроение."

        show jenya smile2 homea at right2 with dissolve
        "Женя поблагодарила меня за утренний сюрприз. Мне кажется, он правда поднял ей настроение."
        hide jenya smile2 homea with dissolve

    else:
        "В дверь постучали."
        "Я открыл дверь, и там стояла Женя. Она всё ещё была уставшей и немного расстроенной."

    #Музыка обычная.
    play music evening
    play background room

    "Мы сели за стол и приступили к учёбе. Я обратил внимание, что Женя очень серьёзная. Она так ответственно и старательно всё делает. Видно было, что, когда что-то не получается, это выводит её из себя."

    scene black with dissolve
    pause 1
    scene bg_room_evening with dissolve

    "Мы делали учебные задания уже несколько часов. Женя не отвлекалась и говорила со мной только по теме. Я решил поинтересоваться, как она себя чувствует."

    show mc normal homea at left2 with dissolve
    stop music fadeout 2.0

    mc "С тобой всё в порядке?"

    show jenya sad homea at right2 with dissolve

    j "Родители опять за своё. А мне очень обидно, у нас постоянные ссоры. Я очень устала, а долги всё не кончаются."

    n "Я заметил, как её глаза начали наполняться слезами."
    play music gone fadein 1.0

    show mc smile homea at left2 with dissolve
    mc "Ну-ну, тихо. С экзаменами у тебя всё будет просто отлично, я в тебя верю. А вот насчёт родителей… надо что-то менять. Может, стоит с ними серьёзно поговорить?"

    if relate_jenya <= 50:
        jump jenya_bad_end
    if relate_jenya > 50:
        jump jenya_good_end
