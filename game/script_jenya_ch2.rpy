
image cj_trashcan_blur = im.Blur("cj/cj_trashcan.jpg", 1.5)

label jenya_cp2:
    $ save_name = _("Глава 2. Женя")

    #заставка

    $ quick_menu = False

    scene black with dissolve

    pause 1

    scene cj_chapter2_start with dissolve

    pause 0.5

    play sound coffee

    scene cj_chapter2 with dissolve

    pause 3

    scene black with dissolve

    play music wholesome

    scene cj_timescip with dissolve

    $ quick_menu = True

    n"Прошло около двух недель. 15 дней, если быть совсем точным. Жизнь текла своим чередом. Моя комната в общаге всё так же оставалась тихим и мрачным местом с графом Дракулой по имени Тимур."

    n"Но даже в тёмном царстве есть лучик света. И моим лучиком была Женя."

    n"Разговоры с ней давали мне понять — я не один. Женя угощала меня кулинарными шедеврами, не давая питаться «мусором». Я даже выучил пару простых рецептов."

    n"Мы обладали талантом привносить в наши планы приключения. Правда, не всегда они заканчивались благополучно, но воспоминания и эмоции того стоили."

    scene cj_trace with dissolve

    n"Ночная леди, посещающая меня, говорила загадками. Порой она говорила и про Женю. У меня возникало ощущение, что незнакомка следит за мной не только во сне."

    n"Уже наяву меня стали преследовать странные происшествия. То стакан упадёт со стола, то дверь сама откроется. А однажды ночью я обнаружил на окне отпечаток руки. Я мог бы спихнуть всё на Тимура, но рука явно была не мужская."

    stop music fadeout 2.5

    scene black with dissolve

    play background room

    play sound signal

    #scene bg_room_morning at pan
    scene bg_room_morning with dissolve

    n"Очередной учебный день. Я уже даже привык к этому жизненному ритму. Но сон для студентов, конечно, больная тема."

    stop sound

    n"Как же тяжело просыпаться к первой паре. Особенно тяжело встать с тёплой постели, когда за окном ещё темень."

    "Наверное у всех после будильника в голове появляется мысль: «А может ну это всё, пропущу одну пару, ничего страшного». Но за одной парой вторая, третья, а там уже тебе грозят незачётом за пропуски."

    "Я взял себя в руки, вылез из-под одеяла и начал свой новый день."

    play music sincerely
    #Спокойная мелодия

    scene bg_street_morning_winter with dissolve
    play background city

    n"Какой же привычной стала необычная для меня раньше дорога в университет. Иду уже на автомате. Хоть тут не надо напрягать моё сонное сознание."

    "Те же магазины, та же дорога. Кажется, что даже прохожие каждый день одинаковые."

    stop music
    scene black with dissolve
    mc "А?"

    play sound tire

    scene bg_street_morning_winter at tremble

    show mc surprise winter with dissolve

    n"Резкий звук."
    play sound bibika

    "Свет фар."

    "Что-то оттянуло меня назад от проезжей части."

    "Моё полусонное состояние как рукой смахнуло. Что произошло?"

    "Я резко оглянулся."

    show mc surprise wintera at left2 with dissolve
    show jenya surprise wintera at right2 with dissolve

    j "Ты вообще смотришь, куда идёшь? Красный на светофоре, ты куда потопал?!"

    n"Тут я всё осознал. Это Женя меня одёрнула. Наверное, здорово напугалась… Как я вообще не заметил светофор?"

    n"Кажется, Женя была напугана не меньше меня. Её глаза были широко раскрыты, а рука всё ещё крепко держала мою."

    n"Я был очень благодарен ей за спасение."

    show mc surprise wintera at left2 with dissolve

    mc "Женя! Как же ты вовремя… Ты меня спасла!"

    show mc normal wintera at left2 with dissolve

    mc "Что-то я совсем не выспался… Эта учёба меня так вымотала."

    show jenya angry wintera at right2 with dissolve

    j "Не выспался? Тебя чуть не убило, дурак!"

    show mc sad wintera at left2 with dissolve

    n"Впервые вижу Женю в таком состоянии. Она явно зла, но в её агрессии можно легко заметить неподдельное волнение."

    n"Мне стало неловко от всей этой ситуации. Если бы не Женя…"

    mc "Извини меня. Я буду внимательнее в следующий раз. Спасибо ещё раз, я твой должник."

    show jenya normal wintera at right2 with dissolve

    n"После моих слов Женя успокоилась. Я был рад, что обстановка разрядилась."

    j "Растяпа… Надо будет тебе лекцию провести о правилах дорожного движения."

    show jenya grin wintera at right2 with dissolve

    j "Но как же круто я тебя спасла! Может, у меня есть суперспособности? Классно я тебя вытянула."

    show mc smile wintera at left2 with dissolve

    n"Мельком пробежал задор на лице Жени. Что ж, это и правда было круто."

    n"В голове промелькнул героический образ Жени с развевающимся плащом и широкой улыбкой."

    mc "Да ты настоящая супер-героиня! Что бы я без тебя делал?"

    mc "Ладно, пойдём скорее, а то сейчас ещё и опоздаем."

    n"Как я рад, что у меня есть Женя. В последнее время я стал ей доверять. Но этот случай дал мне понять одно. Я точно не хочу, чтобы Женя опять так переживала и боялась за меня."

    scene bg_hallway with dissolve
    play music knowledge fadein 1.0
    play background people

    n"До универа мы дошли без происшествий. Постепенно и напряжение начинало уходить. И разговоры о правилах ПДД перетекли в приятную дружескую беседу."

    "Мне нравится болтать с Женей. Кажется, что она может поддержать разговор на любую тему, будь то литература, спорт или новая игра, которой Женя увлеклась в последнее время."

    "Это была какая-то визуальная новелла. Хоть я в подобные игры и не играю, но Женин рассказ меня заинтересовал."

    show jenya smilea  at right2 with dissolve
    show mc smilea  at left2 with dissolve
    j "И оказалось, что всё это время ответы были близко, ведь…"

    stop music
    play music jenyar
    n "Как только зазвонил телефон, Женя резко изменилась в лице."

    show jenya surprisea at right2 with dissolve

    j "Ой! Я же забыла позвонить! Блин, даже не написала сегодня. Только не это…"

    show mc normala at left2 with dissolve

    mc "Что случилось? Что не написала?"

    n "Женя явно напряглась. На экране телефона я увидел контакт «Мама». Неужели это так страшно, что она не написала ей?"

    "Женя ответила на звонок."

    stop music

    j "Да, мамочка… Доброе утро."

    show jenya normala at right2 with dissolve

    j "Прости, совсем закрутилась сегодня…"

    j "Да, я помню, что мы договаривались…"

    show jenya sada at right2 with dissolve

    n "После ответа мамы на той стороне лицо Жени начало принимать более сердитое выражение. Но это не та злость, что я видел утром. В ней нет той заботы, что я чувствовал…"

    "Интересно, что случилось?"

    show jenya angrya at right2 with dissolve

    j "Ну я не пойму… Неужели это так страшно? Я бы точно позвонила тебе после пар!"

    j "Я думала набрать тебе по пути в вуз, но встретила одногруппника и заговорилась с ним, мам!"

    n "Блин, неужели её из-за меня ругают? Я никогда не обсуждал с Женей её семью. Я не знал, что у неё всё так строго. Она же всегда такая активная и весёлая. Вообще не кажется, что у неё могут быть проблемы."

    "Женя увидела моё растерянное лицо и отошла так, что конец этого напряжённого разговора я не услышал."

    show jenya smilea at right2 with dissolve

    "Спустя какое-то время Женя вернулась. Удивительно, но на её лице царила та же улыбка, что и несколькими минутами ранее."

    show mc sad crosseda at left2 with dissolve

    mc "Блин, прости, это из-за меня?"

    j "Да не, не парься."

    n "Я видел, что она что-то недоговаривает. Почему она не хочет поделиться?"

    "Я решил проявить ещё немного напористости."

    show mc talk crosseda at left2 with dissolve

    mc "Точно? Ты в порядке?"

    j "Всё в порядке. Смотри, пара уже начинается!"

    stop background
    play music evening
    scene cj_meet_jenya_angry with dissolve

    n"Пара началась. Женя уткнулась в тетрадь, не обращая внимания ни на препода, ни на меня."

    "Я начал волноваться за её настроение. Неужели она мне не доверяет, раз решила не говорить о своих переживаниях?"

    "Женя выглядит более нервно, чем обычно. Она даже не смотрит в мою сторону, а просто всю пару рисует в своей тетрадке."

    scene cj_meet_jenya_blush with dissolve

    "Несколько раз я пытался подсмотреть, что же она там рисовала. Но все мои попытки были тщетны. Женя отодвинулась от меня подальше и закрыла рисунок рукой."

    scene cj_meet_jenya_draw with dissolve

    "Пара проходила скучнее, чем обычно. Неудивительно, ведь моя подруга все полтора часа отказывалась со мной взаимодействовать. Да и новый материал лекции не запомнился, все мысли заняло странное поведение Жени."

    "Я должен с ней поговорить!"

    stop music fadeout 2.5
    play background room
    scene bg_classroom2 with dissolve

    "Я не мог дождаться окончания пары, чтобы побыстрее пересечься и поговорить с Женей. И, как только стрелка на часах пробила 10:30, Женя быстро выбежала из аудитории."

    "По пути она скомкала и выкинула листок бумаги в мусорку."

    "Я быстро собрал свои вещи и направился к выходу, но остановился у самой двери."

    scene cj_trashcan with dissolve

    n"Передо мной стояло ничем не примечательное мусорное ведро, но главное не то, что снаружи, важно то, что внутри. А внутри лежал тот самый рисунок, что Женя упорно от меня скрывала."

    "Должен ли я…"

    menu:

        "Полезть в мусорку":
            $ vzal = 1

            "Осмотревшись по сторонам, я быстрым движением руки вытащил скомканный листок из ведра."

            "Любопытство сгубило кошку… и мою гордость."

            scene cj_trashcan_blur with dissolve
            play sound bumaga
            show cj_portret with dissolve
            play music sunshine

            n"Я не поверил своим глазам. На тетрадном листе был нарисован мой портрет. Красивый…"

            "Она рисовала это для меня? Но почему же выкинула? Рисунок был замечательный."

            "Не то, чтобы я разбирался в искусстве, да и единственным, что рисовали для меня, были чёрточки в тетради. Такой сюрприз от соседа по парте всегда заканчивался войной — кто больше поначеркает в чужой тетради."

            "Может, Женя и не хотела, чтобы я получил этот рисунок, но выбросить его у меня не поднялась рука. Аккуратно сложив листок, я спрятал его в карман джинсов."

            $ relate_jenya += 15
            if persistent.jenya_frombin != True:
                $ renpy.notify("В дневнике появилась новая запись!")
            $ persistent.jenya_frombin = True
            stop music fadeout 2.0

        "Не лезть в мусорку":
            $ vzal = 0

            n"Нет. Не хватало ещё по мусоркам лазить."

            "Да и если Женя выбросила его, значит, она не хотела, чтобы кто-то это видел."

            "Подавив своё любопытство, я отошёл от мусорки."

    scene bg_hallway with dissolve
    play background people
    play music chill
    #Спокойная музыка.

    n"Я вышел в коридор, выискивая глазами Женю. Долго искать не пришлось."

    "Теперь она выглядит более спокойной, чем пару минут назад. Наверное, ей нужно было время, чтобы прийти в себя. Думаю, стоит завязать разговор на отвлечённую тему."

    "Вообще, насколько я помню, сегодня у нас встреча, организованная Шнурком. Как раз подойду, напомню Жене. Глядишь и диалог завяжется."

    show jenya angrya at right2 with dissolve

    j "Как же я ненавижу эту учёбу, ну почему именно здесь… Сегодня не мой день."

    show mc normala  at left2 with dissolve

    mc "А ты разве поступила не туда, куда хотела?"

    show jenya sada  at right2 with dissolve

    j "Не совсем. Я больше горела желанием идти на графический дизайн. Но родители были не слишком рады и, как всегда, всё решили за меня."

    n"Так вот какая у неё ситуация в семье. Теперь мне хотя бы понятны её эмоции. Как же это тяжело — ходить туда, куда тебя вынудили."

    show mc sad crosseda at left2 with dissolve

    mc "Но всё ведь не может быть настолько плохо?"

    n"Лицо Жени стало мрачнее. Она смотрела в пол и с большой болью продолжала."

    j "Я бы сказала, что они слишком меня опекают… Например, я регулярно должна писать, где я нахожусь, или отчитываться, что я ела на завтрак."

    j "Нет, я, конечно, понимаю, что они меня любят и заботятся обо мне, но это напрягает."

    n "Я молча взял её за руку в знак поддержки. Женя посмотрела на меня и слегка улыбнулась. Кажется, ей стало немного легче, после того, как она рассказала мне о своих переживаниях."

    show jenya normal smilea  at right2 with dissolve

    j "Ладно, давай не будем о грустном. Что же такого важного хочет рассказать наш многоуважаемый староста?"

    show mc normal crosseda  at left2 with dissolve
    show jenya normala  at right2 with dissolve

    mc "Самому интересно. Опять, наверное, про учёбу, долги..."

    "Уже в хорошем настроении мы с Женей двинулись на следующую пару. В этот раз полтора часа пролетели незаметно."

    scene black with dissolve
    #Весёлая музыка.
    play music funny fadein 1.0
    scene cj_alex #at pan
    with dissolve

    a "У меня для вас есть информация…"
    "Преградой к долгожданной свободе стал Шнурок и его «классный час». Своим серьёзным видом Саша действительно напоминал скорее учителя, чем моего одногруппника."

    menu:

        "Слушать":

            a "Некоторые до сих пор не сдали долги за прошлый семестр, настоятельно прошу их сдать, иначе вас отчислят."

            a "А также до сих пор не все предоставили справку о прививках, прошу в течение недели её принести."

            n"Как бы я ни пытался вслушиваться в слова Шнурка, у меня ничего не получалось. Мне сразу вспоминались лекции в прошлом семестре — вроде что-то важное, но как же скучно это говорят."

            "Вернувшись в реальность из своих мыслей, я обнаружил, что староста уже отчитывал двух парней с первого ряда."

            "«Будет сделано, Александр Сергеевич!» - одновременно сказали двое из ларца и продолжили играть в телефонах."

        "Не слушать":

            show bg_classroom with dissolve

            show mc smile crosseda at left2 with dissolve

            mc "Слушай, может, он женится на этой учебе, а?  Он только о ней и говорит."

            show jenya normala at right2 with dissolve

            j "Мне его немного жалко."

            n"Внезапно я заметил, как наша одногруппница пристально смотрит на Шнурка. На самом деле она была чуть ли не единственной, кто слушал каждое его слово."

            show mc talk crosseda at left2 with dissolve

            mc "А что она на него всё время пялится? Сейчас дыру прожжёт."

            show jenya surprisea  at right2 with dissolve

            j "А ты разве не знаешь?"

            n"Я помотал головой от недоумения."

            j "Она влюблена в него. На самом деле, это заметно, если присмотреться."

            show mc smile crosseda at left2 with dissolve

            mc "Да начнутся любовные страсти!"

            show jenya smilea  at right2 with dissolve

            j "Не любитель романтики?"

            n"Я посмотрел на Женю с удивлением."

            "На самом деле, я не против романтики, просто конкретно эта девушка меня пугает. Я в принципе не воспринимаю нормальным любые виды сталкинга."

            show mc normala  at left2 with dissolve

            mc "Да не то, чтобы… Но вот так сталкерить парня и проходу не давать… Со стороны смотрится странно, согласись?"

            n"Женя хмыкнула. Видимо, у нас разные понятия романтики."

    "Спустя ещё несколько минут Шнурок наконец-то нас отпустил. Женя схватила меня под руку и под радостные крики «Свобода!» мы выбежали из аудитории."

    scene black with dissolve
    play music together
    play background city

    scene bg_street_morning_winter with dissolve

    show mc smile wintera at left2 with dissolve

    mc "В моём городе, наверное, сейчас всё белое. Хочу взглянуть на любимый парк. Здесь, в Москве, зима выдалась почти без снега."

    show jenya normal wintera  at right2 with dissolve

    j "Здесь-то без снега? Да у меня в городе бывало пару раз за зиму снег выпадет, и то, сразу тает. А в Москве всё такое снежное."

    mc "Это ты просто не видела настоящей снежной зимы. Будь мы сейчас в моём городе, нам бы пришлось в вуз ползти через сугробы."

    show jenya grin wintera  at right2 with dissolve

    j "Зато, наверное, как красиво, всё белое кругом."

    n "Как же я рад, что мы с Женей дружим. С ней так комфортно, подумал я, и за затянувшимся диалогом не заметил, как мы дошли до общаги."

    stop background
    scene black with dissolve
    scene  bg_room_evening with dissolve
    play music home

    n"Зайдя к себе в комнату, я увидел, что Тимура, как обычно, нет дома."

    "Жаль, что у меня нет возможности с ним как-то поговорить вечером на кухне за кружечкой горячего чая."

    "Ну да ладно, у меня работы предостаточно."

    "Потратив два с половиной часа моих сил на дедлайны, я начал засыпать от усталости."

    "Лучше, конечно же, лечь спать, и с новыми силами и свежей головой снова сесть за дела."

    "Поэтому я незамедлительно рванул в свою тёплую постель и вырубился, только коснувшись подушки."

    scene black with dissolve
    stop music fadeout 2.5
    play background room
    play sound stuk
    scene bg_room_morning with dissolve

    mc "А? Что? Кто здесь?"

    show eve silhouette black glow with dissolve

    n "В дверном проёме своей комнаты я увидел чей-то силуэт. Он пристально смотрел на меня. Я никак не мог разглядеть его и определить, чей же он."

    mc "Кто ты?"
    hide eve silhouette black glow with dissolve

    n"Я решил подойти, чтобы рассмотреть, кто там стоит. Но вдруг чёрный силуэт начал от меня убегать."

    mc "Эй, постой, всё хорошо!"
    scene bg_hallway_dark with dissolve

    play sound steps

    n "Я побежал за силуэтом."

    "Мы бежали по чёрному помещению, пока не открылась какая-то дверь, за которой виднелся свет."

    play sound door
    scene cj_door with dissolve

    "Над ней была цифра 256. Открыв дверь, я проснулся."

    scene  bg_room_dawn #at pan
    with dissolve
    #Спокойная музыка.
    play music home

    n"По моему телу струился холодный пот."

    "Комната 256, что это за комната? Где эта комната? Что это всё значит? Почему мне снятся такие сны?"

    "Вопросы, конечно, были риторические, никто ответа мне не дал. Я решил, что раз проснулся, то пора вставать с кровати."

    "Я оглядел комнату и увидел открытую дверь."

    "Не понял."

    "Я посмотрел на кровать Тимура, который крепко спал. Хм, наверное, это он забыл её закрыть."

    "Тимур всегда поздно возвращается. Видимо, от усталости и не закрыл."

    scene bg_kitchen_morning with dissolve

    n"Выполнив все свои водные процедуры, я направился на кухню."

    "В коридоре я почувствовал приятный запах блинов."
    play background cooking

    "Зайдя на кухню, я был рад снова увидеть Женю, которая готовила завтрак."

    show mc smile homea at left2 with dissolve

    mc "Как вкусно пахнет. Я уже представляю этот вкус."

    show jenya smile3 handsa  at right2 with dissolve

    j "Тоже хочешь? Могу на тебя сделать."

    show mc normal homea at left2 with dissolve

    mc "Если тебе не трудно…"

    show jenya normal homea at right2 with dissolve

    j "Да не, всё нормально."

    show mc normal homea at left2 with dissolve

    mc "Давненько я не ел блины. Ну, с момента, как заехал в общагу."

    show jenya normal smile2 handsa at right2 with dissolve

    j "Тогда привыкай, я часто их готовлю. Блины — моя слабость."

    hide mc normal homea at left2 with dissolve
    hide jenya normal smile2 handsa at right2 with dissolve

    n"Я наблюдал, как горка блинов становилась всё больше. Не зря говорят: «Бесконечно можно смотреть на три вещи: как горит огонь, как течёт вода и как кто-то работает»."
    "И у Жени получалось очень хорошо. Быстрыми отточенными движениями она ловко переворачивала блины один за другим."
    "Может, для многих в этом и нет ничего особенного, но для меня это верх мастерства."
    "Видимо, мастерство моего соседа — это игнорирование окружающего мира. Ну вот, даже сейчас я вспомнил про него."

    stop background fadeout 1.0

    show mc normal homea at left2 with dissolve

    mc "Ты же знаешь моего соседа, да? Ты вроде про него рассказывала."

    show jenya normal handsa at right2 with dissolve

    j "Ну да, а что такое?"

    show mc sad homea at left2 with dissolve

    mc "Да он постоянно спит, а когда я пытаюсь заговорить с ним, молчит. Думал, может, ты про него что-нибудь знаешь."

    j "Думаю, он типичный телец. Они по характеру такие."

    mc "Я вообще в этом не разбираюсь."

    show jenya grin handsa at right2 with dissolve

    j "А кто ты по знаку зодиака? Хочешь, погадаю?"

    n"Мне никто никогда не рассказывал про мой знак зодиака. Я вообще ничего не понимаю в этой теме. Но, наверное, как игра будет прикольно."

    show mc normal homea  at left2 with dissolve

    mc "Я…"

    menu:

            "Козерог":
                j "В этот день не стоит терять надежду и веру в себя, ведь именно она может помочь тебе преодолеть любые трудности."
            "Водолей":
                j "Ты можешь столкнуться с трудностями, но не бойся просить о помощи. Есть люди, которые заботятся о тебе."
            "Рыбы":
                j "Обрати внимание на свои мечты и желания, ведь именно они могут помочь тебе достичь самых амбициозных целей."
            "Овен":
                j "Доверься своим чувствам и знай, что всегда найдешь понимание и помощь в своём окружении."
            "Телец":
                j "В этот день ты можешь встретить свою родственную душу в самом неожиданном месте — например, в парке."
            "Близнецы":
                j "Сегодня тебе стоит быть настороже, ведь твой выбор может изменить твою судьбу."
            "Рак":
                j "Тебя ждёт удачный день, если ты примешь вызов и попробуешь что-то новое."
            "Лев":
                j "Не забывай улыбаться, ведь именно улыбка может стать ключом ко всем твоим успехам и победам."
            "Дева":
                j "Сегодня лучше не пытаться контролировать все аспекты своей жизни, иногда нужно просто расслабиться и насладиться моментом."
            "Весы":
                j "Обрати внимание на свои сны сегодня, ведь именно они могут дать ответы на те вопросы, которые долго мучают тебя."
            "Скорпион":
                j "Будь готов к неожиданным жизненным поворотам, ведь именно они могут привести к самым интересным и необычным событиям."
            "Стрелец":
                j "Сегодня тебе стоит слушать своё сердце и доверять интуиции, ведь именно они могут подсказать верный путь."

    mc "Я не очень верю в такое, но спасибо за совет."

    show jenya normal smile2 handsa at right2 with dissolve
    j "Ты ещё убедишься в достоверности моих гороскопов!"

    n"Женя многозначительно ухмыльнулась. Она источала такую уверенность, что, казалось, она с первой нашей встречи уже знала про меня всё."

    show mc smile homea  at left2 with dissolve

    mc "Я смотрю, ты такой темой увлекаешься."

    j "Да, есть немного. Я ещё и на таро раскладываю, только вот родители…"
    show jenya sad handsa at right2 with dissolve

    n"Я сразу заметил, как лицо Жени поменялось."

    show mc normal homea at left2 with dissolve

    mc "М?"

    j "Они сильно недовольны такими увлечениями. Постоянные упрёки, занудные разговоры… «Хватит ерундой заниматься»."

    show jenya normal homea at right2 with dissolve

    j "Да ничего, я уже привыкла, не о чем беспокоиться."

    n"Как же я хочу, чтобы у неё всё было хорошо в семье. Чтобы в ней царили взаимопонимание и поддержка. Может, я мог бы как-то поддержать её?"

    "Хм, гадание на таро… интересная тема. Может, Женя поможет растолковать мои странные сны? Ещё и смогу её приободрить."

    "Думаю, когда будет более удобный случай, мы ещё вернёмся к этой теме."

    show jenya thoughtful handsa at right2 with dissolve

    j "Пойдём погуляем сегодня в парк?"
    menu:

        "Да":
            scene black with dissolve
            pause 1.0
            $ relate_jenya += 15
            #"Возможно мини-игра."
            #Приятная музыка.
            play music leaves fadein 2.0
            play background park
            scene bg_park_winter with dissolve
            show jenya smile wintera at right2 with dissolve

            j "Как же здесь красиво!"

            show mc smile wintera at left2 with dissolve

            mc "Согласен. С таким человеком рядом вид становится ещё прекраснее."

            show jenya normal smile wintera at right2 with dissolve

            j "Каким?"

            mc "Жизнерадостным, энергичным. Я рад, что мы с тобой познакомились и так сдружились."

            j "Я тоже рада."

            n"Хорошо, что я смог поднять ей настроение."

            "По дороге мы нашли скамейку. Мы оба поняли, что хотим присесть отдохнуть."

            "На улице, конечно, зима, но мороз не обжигает, и гулять можно долго."

            show jenya thoughtful wintera  at right2 with dissolve

            j "Ты не против, если мы порисуем?"

            show mc normal wintera at left2 with dissolve

            mc "Да, без проблем."

            show jenya normal smile wintera  at right2 with dissolve

            n"Мы сидели и рисовали наброски друг друга. Спустя пятнадцать минут работы над нашими скетчами мы решили обменяться блокнотами и посмотреть, что у кого вышло."

            show cj_mcpark with dissolve

            "Женя протянула свой блокнот, на нём был изображён мой портрет. Получилось очень красиво и реалистично."
            hide cj_mcpark with dissolve

            show cj_portret_jenya with dissolve

            "Женя, увидев мой рисунок, улыбнулась. Я, конечно, не такой талантливый художник. Рад, что её позабавили мои каракули."

            hide cj_portret_jenya with dissolve

            mc "А где ты научилась так классно рисовать?"

            show jenya smile wintera at right2 with dissolve

            j "Нигде, я самоучка."

            show mc smile wintera at left2 with dissolve

            mc "Выходит просто великолепно."

            show jenya blush wintera at right2 with dissolve
            n "Женя покраснела."

            j "Спасибо."
            show jenya smile wintera at right2 with dissolve

            if vzal == 1:

                n"Я вспомнил, как Женя рисовала меня на паре. Жаль, что она не знает, что я подобрал тот рисунок."

            if vzal == 0:
                pass
            n"Она такая талантливая. Я даже передать не могу, как мне жаль, что она учится на нелюбимом направлении."

            n"Когда я рассмотрел свой портрет и передал его назад Жене, у меня на коленях оказался её личный стикер. Мелочь, а приятно. Обязательно вклею его в свой дневник."

            stop background fadeout 2.5
            scene black with dissolve
            pause 1.5
            play music home fadein 1.0
            scene bg_hallway_day with dissolve

            n"Погуляв ещё какое-то время, мы вернулись в общежитие."

            "Я как всегда провёл Женю до её комнаты."

            show jenya smilea  at right2 with dissolve

            j "Здорово погуляли!"

            show mc smile handa  at left2 with dissolve

            mc "Это точно. Хорошего вечера."

            j "И тебе."

            n"Я хотел было уже уходить, как Женя меня крепко обняла. Всё произошло так быстро, что я не успел ничего сделать. Пришёл я в себя, когда Женя скрылась за дверью своей комнаты."
            scene bg_room_dawn with dissolve
            "Придя к себе, Тимура я опять не обнаружил."

            "Переодевшись в домашнюю одежду, я налил себе чай и стал рассматривать стикер, который мне подарила Женя. Недолго думая, я достал свой дневник и наклеил стикер на новую страницу. "

            if persistent.jenya_withstick != True:
                $ renpy.notify("В дневнике появилась новая запись!")
            $persistent.jenya_withstick = True

            "Вечером я думал о прогулке, о Женином рисунке и о том прощальном объятии. Понял, что мне очень нравится проводить с Женей время, и я хочу помогать ей, узнавать о её достижениях и любоваться её талантами."

        "Нет":
            show mc normal homea at left2 with dissolve
            mc "Сегодня не смогу, я себя не очень хорошо чувствую, прости."

            j "Ничего страшного, выздоравливай. Хорошего дня!"

            play background room
            play music mystery

            scene bg_hallway_day with dissolve

            n"Проводив Женю, я решил пройти пешком по этажам."

            "Из головы всё не выходил тот сон. Стоило въехать в эту общагу, как вдруг меня начала преследовать всякая чертовщина."

            "Может, я слишком серьёзно к этому отношусь. Сам себе всё напридумывал, теперь жить нормально не могу. Все проблемы у нас в голове. Всё я сам и придумал. И эту девушку, и отпечатки рук, и странные сны…"

            "Как вдруг я заметил дверь комнаты, что была в моём сне. Ещё и с тем же номером: “256”."

            mc "Что за?.."

            scene cj_door2 with dissolve

            n"Я не мог поверить своим глазам. Я стоял и смотрел на ту самую дверь из моего сна."

            "Интересно, там кто-то живёт?"

            "Я решился постучать в дверь."
            play sound stuk
            stop music fadeout 2.0

            "По-видимому, никто не собирался открывать."

            #Фон двери приближается
            "Я приложил ухо к двери, пытаясь хоть что-то услышать. Наверное, со стороны я выгляжу странно, но моё любопытство взяло верх."

            play sound screamer_short
            scene cj_door_girl with dissolve
            #Пугающий, резкий звук.

            "Я отскочил от двери и упал на спину."
            "Дверь открылась. На пороге стояла девушка, живущая в этой квартире."

            un "Привет. Ты пришёл за книгой?"

            n"Я так испугался! Сердце всё ещё бешено билось о грудную клетку."

            "Что? Что вообще за книга? Она, наверное, что-то путает."

            "Я растерялся. Медленно встал и от недоумения не придумал ничего лучше, как взять книгу."

            "Мало ли, подумает ещё что-то не то…"

            mc "А?"

            mc "Я… да, за книгой."

            scene cj_book with dissolve

            mc "А откуда она у тебя?"

            un "Не знаю, когда я сюда въехала, она лежала на столе, а мне чужого добра не надо, своего полно."

            mc "Спасибо."

            #Смена на комфортную музыку

            play music mystery

            scene bg_room_dawn with dissolve

            n"Прибежав в свою комнату, я закрыл дверь. Благо, Тимура уже не было."

            "Переведя дух, я с заинтересованностью начал рассматривать книгу."

            play sound bumaga

            scene cj_number with dissolve

            n"Пролистав все страницы, я увидел, как из книги выпал листок с номером телефона. Рядом было подписано «Вася»."

            "Что это за номер? Зачем я вообще всем этим занимаюсь?"

            "Стоял под дверью из сна, подслушивал, а теперь забрал чужую вещь и копаюсь в ней."
            play sound bumaga
            scene bg_room_dawn with dissolve

            n"Поборов своё любопытство, я положил листок обратно в книгу. Да и что я мог бы сделать? Позвонить по номеру? И что бы я сказал?"

            "«Здравствуйте, Вася! Я нашёл ваш номер в книге, которую мне дала девушка из комнаты, которая мне приснилась». Я и сам ничего бы не понял, поступи мне такой звонок."

    n"Неожиданно моё тело накрыла невероятная усталость. Каждая частичка меня молила прилечь и отдохнуть."

    "Я лёг на кровать и всего на мгновение прикрыл глаза."
    stop music fadeout 2.0
    scene black with off
    pause 1

    #Звук открывающегося окна. - nope
    scene bg_room_morning with dissolve
    play background wind fadein 1.0

    "Что-то прохладно, надо бы окно закрыть."

    "Неужели я всё-таки уснул? Уже темень такая."

    play music darkrooms

    scene cj_night_eve_1 with dissolve
    stop background fadeout 1.0

    n"Я посмотрел на дорогу. Меня передёрнуло от страха."

    n"Вдоль дороги ходила девушка. В ней я сразу узнал свою таинственную преследовательницу."

    mc "Я не могу поверить своим глазам. Это сон? Что происходит?!"
    play sound screamer_short

    scene cj_night_eve_2 at tremble

    n"И тут я вижу на себе взгляд того самого силуэта, который смотрит прямо мне в душу."

    "Но меня же никто не может видеть с улицы! Тем более, сейчас темно. Почему же тогда я ощущаю на себе взгляд?"

    scene bg_room_morning with dissolve

    "Я пулей побежал в свою кровать, зарылся в одеяло и пытался как можно скорее уснуть."
    scene black with dissolve
    pause 1.0

    #Добрая музыка
    play music sincerely fadein 1.0
    #Фон с рассветом в комнате Гг.
    scene bg_room_dawn with dissolve

    n"Я резко открыл глаза. Где я? Это всё ещё сон?"

    "С соседней кровати я услышал скрип пружин. Тимур здесь. Значит, это не сон. Или Тимур умудряется даже у меня во сне спать."

    n "Фух, как же хорошо, что это был лишь сон. Очень реалистичный сон."

    n "Сегодня была жуткая ночь. Несколько странных снов подряд — это уже не просто совпадение."

    "Я так больше не могу. Мне срочно нужно об этом с кем-то поговорить."

    "Я могу доверить всё только одному человеку."

    "Может, Женя и посчитает меня сумасшедшим, но если я ничего не скажу, то из этого точно не выльется ничего хорошего."

    "Я поднял телефон и включил его."

    n"Сейчас всего шесть утра?! Как рано…"

    "Недолго думая, я сразу же спустился к Жене и начал стучать в дверь."
    scene bg_hallway_light with dissolve

    play sound stuk

    "Дверь отворила сонная Женя. Она в недоумении посмотрела на меня."

    show jenya yawn homea at right2 with dissolve

    j "С ума сошёл в такое время будить? Что случилось?"

    show mc surprise homea at left2 with dissolve
    mc "Короче, знаю, звучит очень странно, но мне снится такое…"

    show jenya normal homea at right2 with dissolve

    n"Я рассказал ей про все сны, которые мне снились с момента, как я въехал в общагу. Сон с Жени как рукой сняло."

    "Долго на пороге стоять не пришлось, Женя сразу же потянула меня к себе в комнату."

    scene bg_room_zhenya_dark1 with dissolve

    n"Женя усадила меня на свою кровать. На стенах у неё были развешаны рисунки и плакаты, а возле кровати висела гирлянда. Женя включила её, и комната наполнилась тёплым светом."
    show mc sad homea at left2 with dissolve
    show jenya normal homea at right2 with dissolve

    mc "Я больше не могу молчать, сны просто достали, не могу нормально спать, весь на нервах."

    j "Я хочу тебе помочь, очень рада, что ты пришёл ко мне."

    mc "Мне больше не к кому идти."

    j "Давай посмотрим, что можно сделать. Помоги мне найти вещи для расклада."

    scene black with dissolve #мини-игра на поиск предметов
    label find_game:
        $ hf_init("bg_room_zhenya_dark1", 10,
            ("candle", 580, 565, _("Свеча")),
            ("tablecloth", 335, 845, _("Скатерть")),
            ("incense", 950, 180, _("Благовония")),
            ("amulet", 1600, 630, _("Оберег")),
            mouse=True,
            inventory=False,
            hint=True,
            hover=brightness(.05),
            w=200,
            h=200
        )

        $ hf_bg()
        with dissolve

        centered "{size=+24}{color=#ffffe0}Собери все предметы"

        $ hf_start()

        $ renpy.pause(1, hard=True)

        if hf_return == 0:
            centered "{size=+24}{color=#ffffe0}У тебя получилось!"
        else:
            centered "{size=+24}{color=#ffffe0}Не расстраивайся! \nНе собрано предметов: [hf_return]."
    if hf_return != 0:
        menu:
            "Попробуешь ещё раз?"
            "Ещё раз":
                "Хорошо, давай заново."
                jump find_game

            "Само найдётся":
                $ relate_jenya -= 20
    $ hf_hide()
    with dissolve

    scene cj_divination with dissolve
    play music mystery

    n"Женя принялась за подготовку к гаданию. На прикроватной тумбе она поставила зажжённую свечу и благовония. Воздух в комнате  постепенно заполнился запахом хвои."

    "Тем временем Женя разложила передо мной тёмную скатерть с золотистыми узорами. Достав из мешочка колоду карт, она начала её перетасовывать, что-то нашёптывая."

    "Женя положила на скатерть карты рубашкой вверх."

    j "Вытащи три карты и отдай мне."

    n"Неужели за случайными картинками может быть скрыта моя судьба? Я никогда не верил в подобное, но недавние события заставили меня усомниться в своих суждениях. Может, конечно, это всё глупости… Но почему бы и не попробовать."

    "Нерешительным движением руки я вытащил три случайные карты, не решаясь взглянуть на их лицевую сторону."

    "Вся эта процедура малость затянулась, и я уже не мог дождаться Жениного вердикта. Я был в сильном предвкушении."

    if relate_jenya <= 40:

        scene cj_divination1_2 with dissolve

        j "С тобой постоянно находится девушка… но… она странная. Ты с ней не знаком, но она тебя выбрала. Возможно… девушка мертва и через тебя хочет что-то сказать."

        mc "Я запутался, ничего не понимаю… Рядом девушка, но при этом она мертва?"

        mc "Пожалуйста, что ещё ты можешь сказать? Что будет дальше? Что мне нужно сделать?"

        scene cj_divination1_1 with dissolve

        j "Она безобидна, но, пока ты её не выслушаешь, не уйдет."

        n"Что ж, теперь хотя бы ясно, чего она от меня хочет."

        "А на вопрос кто она, видимо, я сам должен узнать ответ. От этой мысли у меня прошли мурашки по спине. Смогу ли я?"

        mc "Не уверен, что готов с ней разговаривать сейчас. Может, есть способ отложить эту беседу?"

        scene cj_divination1_4 with dissolve

        j "Тебе лучше с этим делом не тянуть. Но я могу дать тебе оберег, с ним призрак тебя не побеспокоит."

        mc "Спасибо большое. Я поговорю с призраком, как только решусь."

    if relate_jenya > 40:
        scene cj_divination2 with dissolve

        j "Вижу, что ты стал более открытым к миру, и тебе это воздастся. Появится очень близкий тебе человек… А может, и уже появился."

        n"За всё время моего пребывания здесь ближе всего я стал с Женей… Оу, вот оно что."

        mc "И что же дальше будет?"

        n"Я удивился своему интересу, но вопрос сам вырвался из меня."

        "Женя смущённо отводила взгляд и мяла в руках краешек темной скатерти. Она уже не смотрела на карты, когда ответила…"

        j "Всё зависит от тебя."

        scene cj_divination3 with dissolve

        n"Женя посмотрела на меня и улыбнулась. Разве я мог не улыбнуться в ответ?"

        "Но неожиданно по моей спине прошёлся холодок. Тот самый, что преследовал меня во снах. Моё тело сковал страх."

        scene cj_divination6 with dissolve

        j "Что с тобой? Что-то не так?"

        n"Конечно, моя резкая смена настроения не могла пройти мимо Жени."

        mc "Всё в порядке. Просто… На мгновение мне показалось, что я опять в том странном сне."

        j "Слушай, не нравится мне это. Давай я тебе оберег дам, хоть поспишь нормально."

        mc "Если это не проблема…"

        scene cj_divination3 with dissolve

        j "Ой, не волнуйся. Главное, чтобы тебе стало легче."

    scene bg_room_zhenya with dissolve
    stop music fadeout 2.5

    n"Женя принялась копошиться в своих вещах. Через мгновение мне вручили оберег."
    play music together fadein 1.0

    scene cj_gift with dissolve
    $ persistent.poster3 = True

    mc "Спасибо. Не знаю, чем тебя и отблагодарить."

    j "Да брось. Я ничего такого и не сделала."

    n"Но я был благодарен Жене не только за гадание и оберег, но и за всё. Надеюсь, и я смогу ей помочь однажды."

    if persistent.jenya_endchap2 != True:
        $ renpy.notify("В дневнике появилась новая запись!")
    $ persistent.jenya_endchap2 = True

    scene bg_room_zhenya with dissolve
    #show mc blush homea at left2 with dissolve - пригодится при оптимизации для веб-версии, как и остальные спрайты, заменённые на ca-версии
    show mc blush home ca at left2 with dissolve

    "Я надел оберег на шею. Странно, но я сразу стал чувствовать себя спокойнее."

    "Правда, мне до сих пор неловко от того, что я так ворвался к Жене в комнату."

    mc "Прости, что я так внезапно вломился, помешал тебе."

    show jenya normal smile handsa at right2 with dissolve

    j "Ничего. Рада, что смогла помочь. Что ещё у тебя нового?"

    #show mc sad homea at left2 with dissolve
    show mc sad home ca at left2 with dissolve

    mc "Сессия… Сильно волнуюсь из-за экзаменов, а тут ещё это."

    show jenya normal smile homea at right2 with dissolve

    j "Понимаю, сама вся на нервах. Но я верю, что мы справимся."

    #show mc smile homea at left2 with dissolve
    show mc smile home ca at left2 with dissolve

    mc "Это само собой. Куда мы денемся."
    #Звук урчания живота

    n"На этот раз это был живот Жени."

    "Но, признаться, я и сам проголодался."

    show jenya smile2 homea at right2 with dissolve

    j "Ой, что-то мы заговорились. Может поедим?"

    n"Как мои мысли прочитала, конечно да! Многомиллионное да!"

    mc "Давай."
    n"Долго мы сидеть не стали и тут же отправились на кухню."

    scene bg_kitchen_morning with dissolve
    show jenya smile2 homea at right2 with dissolve
    #show mc normal homea at left2 with dissolve
    show mc normal home ca at left2 with dissolve

    j "Давай приготовим что-то вместе, ты как раз попрактикуешься."

    #show mc smile homea at left2 with dissolve
    show mc smile home ca at left2 with dissolve

    mc "Хорошо, что ты хотела приготовить?"

    show jenya thoughtful handsa at right2 with dissolve
    j "Думала насчёт пасты карбонара и салата Цезарь."

    #show mc surprise homea at left2 with dissolve
    show mc surprise home ca at left2 with dissolve
    mc "Зачем так сложно? Можно достать котлеты по-киевски из морозилки, я вчера купил."

    show jenya angry handsa at right2 with dissolve

    j "Это же снова полуфабрикаты…. Давай я тебе потом нормальные котлеты сделаю?"

    #show mc blush homea at left2 with dissolve
    show mc blush home ca at left2 with dissolve
    mc "Да не надо."

    n"Спор затянулся на минут пять."

    "Я активно пытался доказать, что незачем заморачиваться, если можно купить уже готовые, замороженные полуфабрикаты и хорошенько их разогреть."

    "Женя же пыталась доказать, что это неполезная еда и что можно расширить ассортимент приготовляемых блюд."

    "Спор, конечно же, никуда не привёл. Каждый из нас был по-своему прав."

    j "Ты совсем не умеешь адекватно питаться."

    #show mc normal homea at left2 with dissolve
    show mc normal home ca at left2 with dissolve

    mc "Нормально я питаюсь."

    n"Пришлось уже согласиться. Но я не мог отрицать, что забота Жени мне очень приятна."

    show jenya smile2 homea at right2 with dissolve
    j "Я пасту быстро сейчас сделаю, ты пока нарежь салат."

    #show mc smile homea at left2 with dissolve
    show mc smile home ca at left2 with dissolve
    mc "Ладно."

    show cj_cooking with dissolve
    play background cooking

    n"Кулинарный процесс активно пошёл."

    "Бурлила вода в кастрюле, шкворчала сковородка, нож стучал по разделочной доске."

    "Страх и тревога от недавнего сна исчезли во время приятной беседы с Женей. А прохладный оберег на груди наполнял меня уверенностью и спокойствием."

    scene black with dissolve

    stop music fadeout 2.0
    stop background fadeout 1.0

    jump jenya_cp3
